apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 14
        versionName '0.10.2'

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true

            versionNameSuffix '-DEVEL'
            applicationIdSuffix '.daily'

            buildConfigField 'String', 'GOOGLE_ANALYTICS_TRACKER', '"UA-68882401-2"'
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true

            buildConfigField 'String', 'GOOGLE_ANALYTICS_TRACKER', '"UA-68882401-2"'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        google
        amazon
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests {
            all {
                // configure the test JVM arguments
                jvmArgs '-noverify'
            }
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    // Android Support libraries
    compile "com.android.support:appcompat-v7:${androidSupportVer}"
    compile "com.android.support:design:${androidSupportVer}"
    compile "com.android.support:cardview-v7:${androidSupportVer}"
    compile "com.android.support:recyclerview-v7:${androidSupportVer}"
    compile "com.android.support:percent:${androidSupportVer}"

    compile 'com.google.android.support:wearable:1.3.0'

    // Google Play Services
    googleCompile "com.google.android.gms:play-services-appinvite:${googlePlayServicesVer}"
    googleCompile "com.google.android.gms:play-services-analytics:${googlePlayServicesVer}"
    googleCompile "com.google.android.gms:play-services-drive:${googlePlayServicesVer}"
    googleCompile "com.google.android.gms:play-services-wearable:${googlePlayServicesVer}"

    // Other libraries
    compile 'io.realm:realm-android:0.87.5'
    compile 'com.github.clans:fab:1.6.2'
    compile 'com.wdullaer:materialdatetimepicker:2.2.0'
    compile 'com.github.PhilJay:MPAndroidChart:v2.2.3'
    compile 'uk.co.chrisjenx:calligraphy:2.1.0'
    compile 'net.danlew:android.joda:2.9.2'
    compile('com.mikepenz:materialdrawer:5.0.5@aar') {
        transitive = true
    }
    compile('com.instabug.library:instabugcompat:2.1.1') {
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    testCompile "junit:junit:${junitVer}"
    testCompile "org.mockito:mockito-all:${mockitoVer}"
    testCompile("com.squareup.assertj:assertj-android:${androidAssertJVer}") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    testCompile("org.robolectric:robolectric:${robolectricVer}") {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    testCompile("org.robolectric:shadows-support-v4:${robolectricVer}") {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }

    wearApp project(':wear')

    // Testing libraries
    androidTestCompile 'com.android.support.test:rules:0.4.1'
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile "com.android.support:support-annotations:${androidSupportVer}"
    androidTestCompile "com.android.support:recyclerview-v7:${androidSupportVer}"
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.1'
}

apply plugin: 'com.google.gms.google-services'